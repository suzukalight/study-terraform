# S3

- プライベート
- パブリック
- 制限付きアクセス

の 3 種類を作っていく。

ファイル名は `s3.tf` で保存する。terraform の場合、基本的にディレクトリにあるすべての設定を実行してくれる。

## プライベートバケット

デフォルトのポリシーは private になる。自動暗号化を ON にしておく；

```hsl
resource "aws_s3_bucket" "private" {
  bucket = "private-pragmatic-terraform-suzukalight"

  versioning {
    enabled = true
  }

  server_side_encryption_configuration {
    rule {
      apply_server_side_encryption_by_default {
        sse_algorithm = "AES256"
      }
    }
  }
}
```

ブロックパブリックアクセスを設定し、予期しないオブジェクトの公開を抑止；

```hsl
resource "aws_s3_bucket_public_access_block" "private" {
  bucket                  = aws_s3_bucket.private.id
  block_public_acls       = true
  block_public_policy     = true
  ignore_public_acls      = true
  restrict_public_buckets = true
}
```

## パブリックバケット

```hsl
resource "aws_s3_bucket" "public" {
  bucket = "public-pragmatic-terraform-suzukalight"
  acl    = "public-read"

  cors_rule {
    allowed_origins = ["https://example.com"]
    allowed_methods = ["GET"]
    allowed_headers = ["*"]
    max_age_seconds = 3000
  }
}
```

## ログバケット

ALB ログを保存する用途で。lifecycle_rule を設定するとログローテーションバケットとして使えるようになる；

```hsl
# log bucket
resource "aws_s3_bucket" "alb_log" {
  bucket = "alb-log-pragmatic-terraform-suzukalight"

  lifecycle_rule {
    enabled = true

    expiration {
      days = "180"
    }
  }
}
```

### バケットポリシー

ALB の場合は AWS が管理しているアカウントから書き込まれるので、そこからのみ PUT 可能にする；

```hsl
## log bukcet policy
data "aws_iam_policy_document" "alb_log" {
  statement {
    effect    = "Allow"
    actions   = ["s3:PutObject"]
    resources = ["arn:aws:s3:::${aws_s3_bucket.alb_log.id}/*"]

    principals {
      type        = "AWS"
      identifiers = ["582318560864"]
    }
  }
}

resource "aws_s3_bucket_policy" "alb_log" {
  bucket = aws_s3_bucket.alb_log.id
  policy = data.aws_iam_policy_document.alb_log.json
}
```

## 実行

```bash
terraform init
terraform plan
terraform apply
```

成功すると、S3 バケットが生成される。（EC2 も引き続きできる）
